buildscript {
    ext.kotlinVersion = '1.2.51'
    ext.ossrhUsername = 'user'
    ext.ossrhPassword = 'password'
}


apply plugin: 'java'
apply plugin: 'maven'

if (project.hasProperty("signing.keyId")) {
    // GRADLE_USER_HOME=/Users/jillesvangurp
    apply plugin: 'signing'
    signing {
        sign configurations.archives
    }
}

group = 'io.inbot'
archivesBaseName = "inbot-testfixtures"
version = '1.7'

description = """Randomized names for companies and persons for use in tests"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Inbot Testfixtures'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'Randomized names for companies and persons for use in tests'
        url 'https://github.com/Inbot/inbot-testfixtures'

        scm {
          connection 'scm:git:git@github.com:inbot/inbot-testfixtures.git'
          developerConnection 'scm:git:git@github.com:inbot/inbot-testfixtures.git'
          url 'https://github.com/Inbot/inbot-testfixtures'
        }

        licenses {
          license {
            name 'MIT License'
            url 'https://github.com/Inbot/inbot-testfixtures/blob/master/LICENSE'
          }
        }

        developers {
          developer {
            id 'jillesvangurp'
            name 'Jilles van Gurp'
          }
        }
      }
    }
  }
}

repositories {
     maven { url "https://oss.sonatype.org/content/groups/public/" }
     maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
     maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    testCompile(group: 'org.testng', name: 'testng', version:'6.9.6') {
			exclude(module: 'junit')
    }
    testCompile group: 'org.assertj', name: 'assertj-core', version:'3.1.0'
    testCompile group: 'org.apache.commons', name: 'commons-lang3', version:'3.4'
}
