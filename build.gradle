apply plugin: 'java'
apply plugin: 'maven'

if (project.hasProperty("signing.keyId")) {
    // GRADLE_USER_HOME=/Users/jillesvangurp
    // add a gradle.properties as described here:
    apply plugin: 'signing'
    signing {
        sign configurations.archives
    }
}

group = 'io.inbot'
archivesBaseName = "inbot-testfixtures"
version = '1.8'

description = """Randomized names for companies and persons for use in tests"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

test {
    useTestNG {
        parallel = "classes"
        threadCount = 15
    }
    testLogging {
        // Make sure output from
        // standard out or error is shown
        // in Gradle output.
        showStandardStreams = true
    }
}
artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty("ossrhUsername")) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Inbot Testfixtures'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'Helper class for human readable randmized names and email addresses'
                    url 'https://github.com/inbot/inbot-testfixtures'

                    scm {
                        connection 'scm:git:git@github.com:inbot/inbot-testfixtures.git'
                        developerConnection 'scm:git:git@github.com:inbot/inbot-testfixtures.git'
                        url 'git://git@github.com:inbot/inbot-testfixtures.git'
                    }

                    licenses {
                        license {
                            name 'MIT license'
                            url 'https://github.com/inbot/inbot-testfixtures/blob/master/LICENSE'
                            distribution 'repo'
                        }
                    }

                    organization {
                        name 'Inbot'
                        url 'http://inbot.io'
                    }

                    developers {
                        developer {
                            id 'jillesvangurp'
                            name 'Jilles van Gurp'
                        }
                    }
                }
            }
        }
    }
}

repositories {
    maven { url "https://oss.sonatype.org/content/groups/public/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile "org.apache.commons:commons-lang3:3.7"
    testCompile "org.testng:testng:6.14.2"
    testCompile "org.assertj:assertj-core:3.9.0"
}
